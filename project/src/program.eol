// CALCULATOR

@cached
operation Calculator declarations(): Collection {
    return self.symbolDeclarations;
}

@cached
operation Calculator symbols(): Collection {
    return self.symbolDeclarations.symbol;
}

@cached
operation Calculator symbolsOfKind(k : Any): Collection {
    return self.declarations().select(d|d.kind=k).symbol;
}

@cached
operation Calculator inputs(): Collection {
    return self.symbolsOfKind(SymbolDeclarationKind#INPUT);
}

@cached
operation Calculator intermediates(): Collection {
    return self.symbolsOfKind(SymbolDeclarationKind#INTERMEDIATE);
}

@cached
operation Calculator outputs(): Collection {
    return self.symbolsOfKind(SymbolDeclarationKind#OUTPUT);
}

@cached
operation Calculator assignmentSteps(): Collection {
    return self.steps.selectByType(AssignmentStep);
}

@cached
operation Calculator hasAssignmentStep(s : Symbol): Boolean {
    return self.assignmentSteps().exists(astep|astep.symbol.name==s.name);
}

// STEPS

operation AssignmentStep toHRN(): String {
    return self.symbol.name + " ≔ " + self.body.toHRN();
}

operation PrintStep toHRN(): String {
    return "debug: show " + self.symbol.name;
}

operation AssignmentStep toJava(): String {
    return self.symbol.name + " = " + self.body.toJava();
}

operation PrintStep toJava(): String {
    return "System.out.println(" + self.symbol.name + ")";
}

// TYPES

@cached
operation PrimitiveType serialize(): String {
	switch (self.primitive) {
		case Primitive#TEXT:
			return "Text";
		case Primitive#REAL:
			return "Real";
		case Primitive#INTEGER:
			return "Integer";
		case Primitive#BOOLEAN:
			return "Boolean";
	}
}

@cached
operation PrimitiveType toJavaType(): String {
	switch (self.primitive) {
		case Primitive#TEXT:
			return "String";
		case Primitive#REAL:
			return "double";
		case Primitive#INTEGER:
			return "int";
		case Primitive#BOOLEAN:
			return "boolean";
	}
}

@cached
operation VectorType serialize(): String {
	return "Vector(" + self.valueType.serialize() + "," + self.length.asString() + ")";
}

@cached
operation VectorType toJavaType(): String {
    return self.valueType.serialize() + "[" + self.length.asString() + "]";
}

operation PrimitiveType isNumeric(): Boolean {
	switch (self.primitive) {
		case Primitive#REAL:
			return true;
		case Primitive#INTEGER:
			return true;
		default:
			return false;
	}
}

operation VectorType isNumeric(): Boolean {
	return false;
}

operation typeEquiv(left : Type, right : Type): Boolean {
	return left.serialize() == right.serialize();
}

operation Type equiv(other : Type): Boolean {
	return typeEquiv(self, other);
}

operation Type isInvalid(): Boolean {
    return false;
}

operation InvalidType isInvalid(): Boolean {
    return true;
}

// EXPRESSIONS
operation primitiveType(p : Any): Type {
	var t = new PrimitiveType();
	t.primitive = p;
	return t;
}

@cached
operation TextLiteral type(): Type {
	// return primitiveType(Primitive#TEXT);
	var t = new PrimitiveType();
	t.primitive = Primitive#TEXT;
	return t;
}

@cached
operation RealLiteral type(): Type {
	return primitiveType(Primitive#REAL);
}

@cached
operation IntegerLiteral type(): Type {
	return primitiveType(Primitive#INTEGER);
}

@cached
operation BooleanLiteral type(): Type {
	return primitiveType(Primitive#BOOLEAN);
}

operation isBoolTy(t : Type): Boolean {
    var boolTy = primitiveType(Primitive#BOOLEAN);
    return boolTy.equiv(t);
}

operation isTextTy(t : Type): Boolean {
    var textTy = primitiveType(Primitive#TEXT);
    return textTy.equiv(t);
}

operation isNumericTy(t : Type): Boolean {
    var realTy = primitiveType(Primitive#REAL);
    var intTy = primitiveType(Primitive#INTEGER);
    if (realTy.equiv(t)) {
        return true;
    } else if (intTy.equiv(t)) {
        return true;
    }
    return false;
}

operation invalidTy(cause : String): InvalidType {
    var invTy = new InvalidType();
    invTy.cause = cause;
    return invTy;
}

operation InvalidType serialize(): String {
    return self.cause;
}

operation InvalidType isNumeric(): Boolean {
    return false;
}

operation isInvalid(t : Type): Boolean {
    return t.isInvalid();
}

operation Expression isWellTyped(): Boolean {
    return (not isInvalid(self.type()));
}

operation SymbolExpression type(): Type {
    return self.symbol.type;
}

operation logIllTypedExpression(e : Expression) {
    ("ill-typed expression found: " + e.toHRN()).println();
}

@cached
operation UnaryExpression type(): Type {
    var bodyTy = self.body.type();
    if (isInvalid(bodyTy)) {
        return bodyTy;
    }
    
    switch (self.operator) {
        case UnaryOp#LOGICAL_NOT:
            if (isBoolTy(bodyTy)) {
                return bodyTy;
            } 
            return invalidTy("logical negation expects boolean operand");
        case UnaryOp#NEGATION:
            if (isNumericTy(bodyTy)) {
                return bodyTy;
            }
            return invalidTy("negation expects numeric operand");
    }
}

@cached
operation BinaryExpression type(): Type {
    var leftTy = self.exprs[0].type();
    if (isInvalid(leftTy)) {
        return leftTy;
    }
    
    var rightTy = self.exprs[1].type();
    if (isInvalid(rightTy)) {
        return rightTy;
    }
    
    switch (self.operator) {
        case BinaryOp#ADDITION:
            if (leftTy.equiv(rightTy) and (isNumericTy(leftTy) or isTextTy(leftTy))) {
                return leftTy;
            }
            return invalidTy("both operands of an addition/catenation must be numerics/text");
        case BinaryOp#MULTIPLICATION:
            continue;
        case BinaryOp#DIVISION:
            continue;
        case BinaryOp#SUBTRACTION:
            continue;
        case BinaryOp#POWER: // TODO FIXME: should power only apply to real numbers?
            continue;
        case BinaryOp#MODULO:
            continue;
        case BinaryOp#GREATER_THAN:
            continue;
        case BinaryOp#GREATER_THAN_EQUAL_TO:
            continue;
        case BinaryOp#LESS_THAN:
            continue;
        case BinaryOp#LESS_THAN_EQUAL_TO:
            if (leftTy.equiv(rightTy) and isNumericTy(leftTy)) {
                return leftTy;
            }
            ("op: " + self.operator).println();
            ("leftTy: " + leftTy.toHRN()).println();
            ("rightTy: " + rightTy.toHRN()).println();
            ("expr: " + self.toHRN()).println();
            return invalidTy("both operands of mul/sub/pow expected to be the same numeric type");
        case BinaryOp#LOGICAL_AND:
            continue;
        case BinaryOp#LOGICAL_OR:
            continue;
        case BinaryOp#LOGICAL_IMPLIES:
            if (isBoolTy(leftTy) and isBoolTy(rightTy)) {
                return leftTy;
            }
            return invalidTy("both operands of and/or/implies are expected to be boolean-typed");      
        case BinaryOp#EQUIVALENT:
            continue;
        case BinaryOp#NOT_EQUIVALENT:
            if (leftTy.equiv(rightTy)) {
                return primitiveType(Primitive#BOOLEAN);
            }
            return invalidTy("both operands of equivalence expected to be of the same type");
    }
}

@cached
operation TertiaryExpression type(): Type {
    var fstTy = self.exprs[0].type();
    var sndTy = self.exprs[1].type();
    var thdTy = self.exprs[2].type();

    if (isInvalid(fstTy)) {
        return fstTy;
    }

    if (isInvalid(sndTy)) {
        return sndTy;
    }

    if (isInvalid(thdTy)) {
        return thdTy;
    }

    switch (self.operator) {
        case TertiaryOp#IF_THEN_ELSE:
            if (not isBoolTy(fstTy)) {
                return invalidTy("condition of if/then/else must be boolean typed");
            }

            if (sndTy.equiv(thdTy)) {
                return sndTy;
            }
            return invalidTy("operands of if/then/else must be of the same type");
    }
}

operation TextLiteral toHRN(): String {
    return self.value;
}

operation IntegerLiteral toHRN(): String {
    return self.value.asString();
}

operation RealLiteral toHRN(): String {
    return self.value.asString();
}

operation BooleanLiteral toHRN(): String {
    return self.value.asString().toUpperCase();
}

operation SymbolExpression toHRN(): String {
    return self.symbol.name;
}

@cached
operation UnaryExpression toHRN(): String {
    var op = "";
    switch (self.operator) {
        case UnaryOp#LOGICAL_NOT:
            op = "¬";
        case UnaryOp#NEGATION:
            op = "-";
    }
    return op + " (" + self.body.toHRN() + ")";
}


@cached
operation BinaryExpression toHRN(): String {
    var left = self.exprs[0].toHRN();
    var right = self.exprs[1].toHRN();
    var op = "";

    switch (self.operator) {
        case BinaryOp#ADDITION:
            op = "+";
        case BinaryOp#MULTIPLICATION:
            op = "×";
        case BinaryOp#DIVISION:
            return "(" + left + ") / (" + right + ")";
        case BinaryOp#SUBTRACTION:
            op = "-";
        case BinaryOp#POWER:
            op = "^";
        case BinaryOp#MODULO:
            op = "%";
        case BinaryOp#GREATER_THAN:
            op = ">";
        case BinaryOp#GREATER_THAN_EQUAL_TO:
            op = "≥";
        case BinaryOp#LESS_THAN:
            op = "<";
        case BinaryOp#LESS_THAN_EQUAL_TO:
            op = "≤";
        case BinaryOp#LOGICAL_AND:
            op = "∧";
        case BinaryOp#LOGICAL_OR:
            op = "∨";
        case BinaryOp#LOGICAL_IMPLIES:
            op = "⇒";
        case BinaryOp#EQUIVALENT:
            op = "=";
        case BinaryOp#NOT_EQUIVALENT:
            op = "≠";
    }

    return "(" + left + " " + op + " " + right + ")";
}

@cached
operation TertiaryExpression toHRN(): String {
    var fst = self.exprs[0].toHRN();
    var snd = self.exprs[1].toHRN();
    var thd = self.exprs[2].toHRN();

    switch (self.operator) {
        case (TertiaryOp#IF_THEN_ELSE):
            return "(if " + fst + " then " + snd + " else " + thd + ")";
    }
}

@cached
operation TextLiteral symbolReferences(): Collection {
    return Sequence{};
}

@cached
operation RealLiteral symbolReferences(): Collection {
    return Sequence{};
}

@cached
operation IntegerLiteral symbolReferences(): Collection {
    return Sequence{};
}

@cached
operation BooleanLiteral symbolReferences(): Collection {
    return Sequence{};
}

@cached
operation SymbolExpression symbolReferences(): Collection {
    var c = Sequence{};
    c.add(self.symbol);
    return c;
}

@cached
operation UnaryExpression symbolReferences(): Collection {
    return self.body.symbolReferences();
}

@cached
operation BinaryExpression symbolReferences(): Collection {
    var c = self.exprs[0].symbolReferences();
    c.addAll(self.exprs[1].symbolReferences());
    return c;
}

@cached
operation TertiaryExpression symbolReferences(): Collection {
    var c = self.exprs[0].symbolReferences();
    c.addAll(self.exprs[1].symbolReferences());
    c.addAll(self.exprs[2].symbolReferences());
    return c;
}

// EVALUATION SIMULATION
operation TextLiteral evaluate(scope : Map): Any {
    return self.value;
}

operation RealLiteral evaluate(scope : Map): Any {
    return self.value;
}

operation IntegerLiteral evaluate(scope : Map): Any {
    return self.value;
}

operation BooleanLiteral evaluate(scope : Map): Any {
    return self.value;
}

operation SymbolExpression evaluate(scope : Map): Any {
    return scope.get(self.symbol.name);
}

operation UnaryExpression evaluate(scope : Map): Any {
    var value = self.body.evaluate(scope);

    switch (self.operator) {
        case UnaryOp#LOGICAL_NOT:
            value = not value;
        case UnaryOp#NEGATION:
            value = - value;
    }

    return value;
}

operation BinaryExpression evaluate(scope : Map): Any {
    var left = self.exprs[0].evaluate(scope);
    var right = self.exprs[1].evaluate(scope);
    var value = null;

    switch (self.operator) {
	    case BinaryOp#ADDITION:
            value = left + right;
	    case BinaryOp#MULTIPLICATION:
            value = left * right;
	    case BinaryOp#DIVISION:
            value = left / right; // TODO: be careful here
	    case BinaryOp#SUBTRACTION:
            value = left - right;
	    case BinaryOp#POWER:
            value = left.pow(right); // TODO: be careful here
        case BinaryOp#MODULO:
            value = left.mod(right);
        case BinaryOp#GREATER_THAN:
            value = left > right;
        case BinaryOp#GREATER_THAN_EQUAL_TO:
            value = left >= right;
        case BinaryOp#LESS_THAN:
            value = left < right;
        case BinaryOp#LESS_THAN_EQUAL_TO:
            value = left <= right;
	    case BinaryOp#LOGICAL_AND:
            value = left and right;
	    case BinaryOp#LOGICAL_OR:
            value = left or right;
	    case BinaryOp#LOGICAL_IMPLIES:
            value = (not left) or right;
	    case BinaryOp#EQUIVALENT:
            value = left == right;
        case BinaryOp#NOT_EQUIVALENT:
            value = left != right;
    }

    return value;
}

operation TertiaryExpression evaluate(scope : Map): Any {
    var fst = self.exprs[0].evaluate(scope);
    var value = null;

    switch (self.operator) {
        case TertiaryOp#IF_THEN_ELSE:
            if (fst) {
                value = self.exprs[1].evaluate(scope);
            } else {
                value = self.exprs[2].evaluate(scope);
            }
    }

    return value;
}

operation AssignmentStep simulate(scope : Map): Map {
    scope.put(self.symbol.name, self.body.evaluate(scope));
    return scope;
}

operation PrintStep simulate(scope : Map): Map {
    var stub = self.symbol.name + " = " + scope.get(self.symbol.name).asString();
    if (self.preface?.length() > 0) {
        stub = stub + " ~ " + self.preface;
    }
    stub.println();
    return scope;
}

operation TestCase isSatisfied(progSteps : Collection): Boolean {
    // Create scope map
    var emptyScope = Map{};
    var scope = Map{};
    for (asgn in self.assignments) {
        scope.put(asgn.symbol.name, asgn.body.evaluate(emptyScope)); // note: each input should have an empty scope!
    }
    ("Testing assertion: " + self.name + ", with input set: " + scope.asString()).println();   
    
    // Simulate algorithm steps
    for (step in progSteps) {
        ("Simulating step: " + step.toHRN()).println();
        scope = step.simulate(scope);
    }

    // Evaluate assertions on the scope and ensure that they are all satisfied
    var allAssertionsSatisfied = true;
    for (assertable in self.assertions) {
        ("Checking assertion: " + assertable.toHRN() + "... ").print();
        if (assertable.evaluate(scope)) {
            "Ok!".println();
        } else {
            "Failed!".println();
            allAssertionsSatisfied = false;
            break;
        }
    }

    if (allAssertionsSatisfied) {
        ("Test case " + self.name + " SUCCEEDED!").println();
    } else {
        ("Test case " + self.name + " FAILED!").println();
    }

    return allAssertionsSatisfied;
}

operation String withDefault(s : String): String {
    return self.length() == 0 ? s : self;
}

operation PrintStep dependencies(): Collection {
    return Sequence{self.symbol};
}

operation PrintStep calculates(): Collection {
    return Sequence{};
}

operation AssignmentStep dependencies(): Collection {
    return self.body.symbolReferences();
}

operation AssignmentStep calculates(): Collection {
    return Sequence{self.symbol};
}

operation Calculator checkStepDependencies(): Boolean {
    var ok = true;
    var assigned = self.inputs().clone();

    for (step in self.steps) {
        if (not assigned.includesAll(step.dependencies())) {
            ok = false;
            break;
        }
        assigned.addAll(step.calculates());
    }

    return ok;
}

operation TextLiteral toJava(): String {
    return self.value;
}

operation IntegerLiteral toJava(): String {
    return self.value.asString();
}

operation RealLiteral toJava(): String {
    return self.value.asString();
}

operation BooleanLiteral toJava(): String {
    return self.value.asString();
}

operation SymbolExpression toJava(): String {
    return self.symbol.name;
}

@cached
operation UnaryExpression toJava(): String {
    var op = "";
    switch (self.operator) {
        case UnaryOp#LOGICAL_NOT:
            op = "!";
        case UnaryOp#NEGATION:
            op = "-";
    }
    return op + " (" + self.body.toJava() + ")";
}


@cached
operation BinaryExpression toJava(): String {
    var left = self.exprs[0].toJava();
    var right = self.exprs[1].toJava();
    var op = "";

    switch (self.operator) {
        case BinaryOp#ADDITION:
            op = "+";
        case BinaryOp#MULTIPLICATION:
            op = "*";
        case BinaryOp#DIVISION:
            return "(" + left + ") / (" + right + ")";
        case BinaryOp#SUBTRACTION:
            op = "-";
        case BinaryOp#POWER:
            op = "^"; // todo: power
        case BinaryOp#MODULO:
            op = "%";
        case BinaryOp#GREATER_THAN:
            op = ">";
        case BinaryOp#GREATER_THAN_EQUAL_TO:
            op = ">=";
        case BinaryOp#LESS_THAN:
            op = "<";
        case BinaryOp#LESS_THAN_EQUAL_TO:
            op = "<=";
        case BinaryOp#LOGICAL_AND:
            op = "&&";
        case BinaryOp#LOGICAL_OR:
            op = "||";
        case BinaryOp#LOGICAL_IMPLIES:
            op = "⇒"; // todo: implies
        case BinaryOp#EQUIVALENT:
            op = "==";
        case BinaryOp#NOT_EQUIVALENT:
            op = "!=";
    }

    return "(" + left + " " + op + " " + right + ")";
}

@cached
operation TertiaryExpression toJava(): String {
    var fst = self.exprs[0].toJava();
    var snd = self.exprs[1].toJava();
    var thd = self.exprs[2].toJava();

    switch (self.operator) {
        case (TertiaryOp#IF_THEN_ELSE):
            return fst + " ? " + snd + " : " + thd;
    }
}
